#!/usr/bin/env perl
use strict;
use warnings;

use File::Slurp qw/read_file/;

my $user = shift(@ARGV) || $ENV{USER} || 'camelia';
my $home = glob "~$user";
my $version_file = "$home/rakudo-inst/revision";


my $token = 'p6eval-token';

my $last_launch   = time;
my $restart_every = 4 * 3600;
my $pid = launch();
my $version = read_file($version_file);

$SIG{INT} = sub { kill 9, $pid };
$SIG{HUP} = sub {
    kill 9, $pid;
    exec $^X, $0, @ARGV;
    exit;
};

while (1) {
    sleep 60;
    my $new_version = read_file($version_file);
    next if $new_version eq $version && (time - $last_launch) >= $restart_every;

    kill 9, $pid;
    wait;
    $pid = launch();
    $version = $new_version;
}

sub launch {
    my $pid = fork;
    $last_launch = time;
    if (!defined $pid) {
        die "Cannot fork: $!";
    }
    elsif ($pid) {
        # parent
        sleep 5;
        chmod 0666, $token;
        return $pid;
    }
    else {
        # child
        unlink $token;
        exec "$home/rakudo-inst/bin/perl6-eval-server",  "-cookie", $token, "-app",
            "$home/rakudo-inst/share/perl6/runtime/perl6.jar";
    }
}
